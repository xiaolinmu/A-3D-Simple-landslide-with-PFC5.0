new
set random 10001

title "Studies on Landslide Properties"

domain extent -5 5 
cmat default model linear property kn 1e6

[h = 0.5]
[sita = 45]
[h0 = 0.25] 
[lenbot = 0.2]
[lenfb = 0.4]
[detax = 0.1]
[detaz = 0.1]

[x = array.create(1,6)]
[y = array.create(1,2)]
[z = array.create(1,3)]

;[a = math.tan(45*math.pi/180)]
def slidepnt(h, sita, h0)
    x[1,1] = -lenbot/2.0-h/math.tan(sita*math.pi/180)
    x[1,2] = -lenbot/2.0
    x[1,3] = lenbot/2.0
    x[1,4] = lenbot/2.0+h/math.tan(sita*math.pi/180)
    x[1,5] = -lenbot/2.0-(h-h0)/math.tan(sita*math.pi/180)
    x[1,6] = -lenbot/2.0-h/math.tan(sita*math.pi/180)+h0/(math.sin(sita*math.pi/180)*math.cos(sita*math.pi/180))
    y[1,1] = -lenfb/2
    y[1,2] = lenfb/2
    z[1,1] = 0.0
    z[1,2] = h
    z[1,3] = h-h0
end
@slidepnt(@h, @sita, @h0)

;生成源头坡
wall generate ...
    group boundaries ...
    id 1 ...
    name orislide ...
    polygon ...
        @x(1,2) @y(1,1) @z(1,1) ...
        @x(1,2) @y(1,2) @z(1,1) ...
        @x(1,1) @y(1,2) @z(1,2) ...
        @x(1,1) @y(1,1) @z(1,2)

;生成对岸坡
wall generate ...
    group boundaries ...
    id 2 ...
    name opposlide ...
    polygon ...
        @x(1,3) @y(1,1) @z(1,1) ...
        @x(1,3) @y(1,2) @z(1,1) ...
        @x(1,4) @y(1,2) @z(1,2) ...
        @x(1,4) @y(1,1) @z(1,2)

;生成底坡
wall generate ...
    group boundaries ...
    id 3 ...
    name botslide ...
    polygon ...
        @x(1,2) @y(1,1) @z(1,1) ...
        @x(1,2) @y(1,2) @z(1,1) ...
        @x(1,3) @y(1,2) @z(1,1) ...
        @x(1,3) @y(1,1) @z(1,1)

;生成前约束
wall generate ...
    group boundaries ...
    id 4 ...
    name frontslide ...
    plane ...
        dip 90.0 ...
        ddir 0.0 ...
        position @x(1,2) @y(1,1) @z(1,1)   

;生成后约束
wall generate ...
    group boundaries ...
    id 5 ...
    name backslide ...
    plane ...
        dip 90.0 ...
        ddir 0.0 ...
        position @x(1,2) @y(1,2) @z(1,1) 

;生成料斗面
wall generate ...
    group boundaries ...
    id 6 ...
    name ldslide ...
    polygon ...
        @x(1,5) @y(1,1) @z(1,3) ...
        @x(1,5) @y(1,2) @z(1,3) ...
        @x(1,6) @y(1,2) @z(1,2) ...
        @x(1,6) @y(1,1) @z(1,2)

wall activeside both
;颗粒级配曲线
def granulometry
    global lintab = table.create("experimental")
    table(lintab, 0.005) = 0.0
    table(lintab, 0.01) = 0.25
    table(lintab, 0.02) = 0.60
    table(lintab, 0.04) = 1.00
end
@granulometry

[table.y(lintab,2)]
;pause key

;生成粒子
ball distribute box [x(1,1)+detax] [x(1,6)-detax] [y(1,1)] [y(1,2)] [z(1,3)] [z(1,2)+detaz] ...
                porosity 0.5 ...
                numbin 3 ...
                bin 1 ...
                    radius [0.5*table.x(lintab, 1)] [0.5*table.x(lintab, 2)] ...
                    volumefraction [table.y(lintab,2)-table.y(lintab,1)] ...
                    group smallball ...
                bin 2 ...
                    radius [0.5*table.x(lintab, 2)] [0.5*table.x(lintab, 3)] ...
                    volumefraction [table.y(lintab,3)-table.y(lintab,2)] ...
                    group mediumball ...
                bin 3 ...
                    radius [0.5*table.x(lintab, 3)] [0.5*table.x(lintab, 4)] ...
                    volumefraction [table.y(lintab,4)-table.y(lintab,3)] ...
                    group largeball

ball delete range ...
        plane ...
            origin ...
                [x(1,5)] [y(1,1)] [z(1,3)] ...
            dip [sita] ...
            dd 90 ...
            below

ball delete range ...
        plane ...
            origin ...
                [x(1,5)] [y(1,1)] [z(1,3)] ...
            dip [sita] ...
            dd 90 ...
            distance 0.02 

ball delete range ...
        plane ...
            origin ...
                [x(1,5)] [y(1,1)] [z(1,3)] ...
            dip [-sita] ...
            dd 90 ...
            below

ball delete range ...
        plane ...
            origin ...
                [x(1,5)] [y(1,1)] [z(1,3)] ...
            dip [-sita] ...
            dd 90 ...
            distance 0.02 

ball delete range ...
        plane ...
            origin ...
                [x(1,5)] [y(1,1)] [z(1,3)] ...
            dip 0 ...
            dd 0 ...
            distance 0.02

ball delete range ...
        plane ...
            origin ...
                [x(1,5)] [y(1,2)] [z(1,3)] ...
            dip 0 ...
            dd 0 ...
            distance 0.02 

;料斗内颗粒堆积
ball attribute density 2650 damp 0.05

cycle 10000 calm 10
save elemade1    
    





